* Chorismate mutase with chorismate
******
* Calculation of AM1 energies of QM-atoms only, with Arg90 side chain included
*
! This script is for only one structure, set R to read in desired one.

bomlev -2

set R -0.3

! Read in topology files
! Standard topology file for proteins
read rtf card name top_all22_prot.inp

! Topology file of ligand (chorismate)
read rtf card name chor.rtf append

! Patches for neutralized Arg, Glu, Lys & Asp
read rtf card name res_neutral.rtf append

!! Add link-atom atom-type
read rtf card append
* Add atom-type for link-atoms
22 2
MASS  200  QQ    1.00800  H ! Link atom for QM/MM
end

! Read in parameter files
! Standard parameter file for protein
read para card name par_all22_prot.inp

! Parameter file for ligand (chorismate)
read para card name chor.prm append

! Read parameters for link-atom
read para card name linkatom27.prm append

! Read PSF file of 25 Ang sphere of solvated CM with chorismate
read psf card name cmchor_25.psf

! Add link-atom
ADDLinkatom QQ90 MB 90 CD MB 90 CG
!RELLinkatom QQ90 MB 90 CD MB 90 CG

! RELLinkatom is NOT used, because it does not have to be placed; 
! coordinates are read in below. Because there are no coordinates present yet,
! using this command will cause an error because charmm 'thinks' 
! all atoms have the same coordinates

! delete all MM atoms
DELE ATOM SELE .NOT. (SEGID CH -    
 .OR. (ATOM MB 90 QQ90) .OR. (ATOM MB 90 CD) .OR. (ATOM MB 90 HD1) -
 .OR. (ATOM MB 90 HD2) .OR. (ATOM MB 90 NE) .OR. (ATOM MB 90 HE) -
 .OR. (ATOM MB 90 CZ) .OR. (ATOM MB 90 NH1) .OR. (ATOM MB 90 HH11) -
 .OR. (ATOM MB 90 HH12) .OR. (ATOM MB 90 NH2) .OR. (ATOM MB 90 HH21) -
 .OR. (ATOM MB 90 HH22)) END

! Set QM-region (chorismate and Arg90 side chain), define method and charge
QUANTUM SELE SEGID CH - 
 .OR. (ATOM MB 90 QQ90) .OR. (ATOM MB 90 CD) .OR. (ATOM MB 90 HD1) -
 .OR. (ATOM MB 90 HD2) .OR. (ATOM MB 90 NE) .OR. (ATOM MB 90 HE) -
 .OR. (ATOM MB 90 CZ) .OR. (ATOM MB 90 NH1) .OR. (ATOM MB 90 HH11) -
 .OR. (ATOM MB 90 HH12) .OR. (ATOM MB 90 NH2) .OR. (ATOM MB 90 HH21) -
 .OR. (ATOM MB 90 HH22) END - 
REMO AM1 ITRM 1000 CHARGE -1.0 EXGR

! read minimized CM+chorismate conformation
read coor card name qm_am1r90_rc@R.crd

! Do energy calculation
ENER

STOP



